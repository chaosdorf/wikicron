#!/usr/bin/env perl
## Copyright © 2015 by Daniel Friesel <derf@chaosdorf.de>
## License: WTFPL:
##   0. You just DO WHAT THE FUCK YOU WANT TO

use strict;
use warnings;
use 5.012;
use utf8;

use DateTime;
use Encode qw(decode encode);
use File::Slurp qw(read_file write_file);
use Git;

sub update_plenum {
	my ( $filename, $this_week_dt, $next_week_dt ) = @_;

	my $this_week_link = $this_week_dt->strftime('%Y/%m-%d');
	my $next_week_link = $next_week_dt->strftime('%Y/%m-%d');

	my $this_week_text = $this_week_dt->strftime('%d.%m.');
	my $next_week_text = $next_week_dt->strftime('%d.%m.');

	my $plenum_site = decode( 'utf8', read_file($filename) );
	$plenum_site
	  =~ s{ \[Plenum \s diese \s Woche \] \( /plenum/entry/..../..-.. \) (?: \s \(\d+\.\d+\.\))? }
		{[Plenum diese Woche](/plenum/entry/$this_week_link) ($this_week_text)}x;
	$plenum_site
	  =~ s{ \[Plenum \s nächste \s Woche \] \( /plenum/entry/..../..-.. \) (?: \s \(\d+\.\d+\.\))? }
		{[Plenum nächste Woche](/plenum/entry/$next_week_link ($next_week_text)}x;

	write_file( $filename, encode( 'utf8', $plenum_site ) );
}

our $VERSION = '0.0';

my $this_week_dt
  = DateTime->now( time_zone => 'Europe/Berlin' )->add( days => 1 );
my $next_week_dt = $this_week_dt->clone->add( days => 7 );

if ( $this_week_dt->day_of_week != 2 ) {
	die( 'Target date is ' . $this_week_dt->day_name . ", not Tuesday!\n" );
}

chdir('/home/derf/var/svn/intern.chaosdorf.de')
  or die("Failed to chdir\n");

my $repo = Git->repository();

$repo->command_noisy('pull');

update_plenum( 'verein/plenum.md', $this_week_dt, $next_week_dt );
update_plenum( 'home.md',          $this_week_dt, $next_week_dt );

$repo->command_noisy( 'add',    'verein/plenum.md' );
$repo->command_noisy( 'add',    'home.md' );
$repo->command_noisy( 'commit', '-m', 'Plenums-Shortcuts' );
$repo->command_noisy('push');
