#!/usr/bin/env perl
## Copyright © 2012,2013 by Daniel Friesel <derf@chaosdorf.de>
## License: WTFPL:
##   0. You just DO WHAT THE FUCK YOU WANT TO

use strict;
use warnings;
use 5.012;
use autodie;
use utf8;

use Data::Dumper;
use DateTime;
use MediaWiki::API;

our $VERSION = '0.0';

my $prefix = '/tmp/wfs/chaosdorf';

my $date = DateTime->now( time_zone => 'Europe/Berlin' )
  ->add( DateTime::Duration->new( days => 63 ) );

if ( $date->day_of_week != 5 ) {
	die( 'Target date is ' . $date->day_name . ", not Friday!\n" );
}

my $ymd    = $date->ymd;
my $target = "Freitagsfoo/${ymd}";

my $content = <<"EOF";
{{Event
 |  Title = Freitagsfoo
 |  Description = wöchentliches öffentliches Treffen
 |  Type = Meet-Up
 |  Date = ${ymd}
 |  Start = 18:00
 |  Duration = 6
 |  Host = FIXME
 |  isRelevant = Yes
}}
{{Freitagsfoo}}

Alle Anwesenden sind eingeladen, kurze Vorträge zu für sie interessanten Themen
zu halten. Die Vorträge beginnen meist gegen 21 Uhr; ein einzelner Vortrag
sollte nicht länger als 20 Minuten sein.

[https://wiki.chaosdorf.de/index.php?title=Freitagsfoo/${ymd}&amp;action=edit&amp;section=new add your talk here]
EOF

my $mw = MediaWiki::API->new(
{
	api_url => 'https://wiki.chaosdorf.de/api.php',
	on_error => \&on_error
}
);

sub on_error {
	print 'Error code: ' . $mw->{error}->{code} . "\n";
	print $mw->{error}->{stacktrace} . "\n";
	exit 1;
}

$mw->login(
{
	lgname     => $ENV{WIKI_USER},
	lgpassword => $ENV{WIKI_PASSWORD},
	lgdomain   => 'local',
}
);
my $ts = $mw->get_page( {title => $target} )->{timestamp};
$mw->edit( {
	action => 'edit',
	title => $target,
	basetimestamp => $ts,
	text => $content,
	bot => 1,
} )
|| die $mw->{error}->{code} . ':'.$mw->{error}->{details};
