#!/usr/bin/env perl
## Copyright © 2012,2013 by Daniel Friesel <derf@chaosdorf.de>
## License: WTFPL:
##   0. You just DO WHAT THE FUCK YOU WANT TO

use strict;
use warnings;
use 5.010;
use utf8;

use DateTime;
use Encode qw(decode encode);
use Email::Sender::Simple qw(sendmail);
use Email::Simple;
use Email::Simple::Creator;
use File::Slurp;
use Git;
use MIME::Words qw(encode_mimewords);

our $VERSION = '0.0';

my $re_ignored = qr{
	^ (?: Bürgerliche \s Namen \s verbleiben \s nicht \s im \s Protokoll )
	| (?: Absatz \) \s beim \s Verfassen )
	| (?: Sonntag, \s 15:00 \s Uhr )
	| (?: \#\# \s Nickname \s )
	| (?: \#\# \s Titel \s des \s (?: Berichts | Beschlusses ) )
	| (?: > \s Ich \s habe \s zusammen \s mit \s \[ person \] )
	| (?: Deadline: \s Sonntag )
	| (?: Danach \s bitte \s keine )
	| (?: Hier \s Vorschläge \s aufschreiben )
	| (?: Formulierung \s beachten, \s alle )
	| (?: Vorschlag: \s Dinge \s von \s Relevanz )
	| (?: Ende: \s \[..:..\] )
}x;

my $date = DateTime->now( time_zone => 'Europe/Berlin' )
  ->add( DateTime::Duration->new( days => 2 ) );

chdir('/home/derf/var/svn/intern.chaosdorf.de')
  or die("Failed to chdir\n");

my $ymd  = $date->strftime('%Y/%m-%d');
my $dmy  = $date->strftime('%d.%m.%Y');
my $repo = Git->repository();

$repo->command_noisy('pull');

my $body
  = "Das nächste Plenum findet am ${dmy} in <https://wiki.chaosdorf.de/VirtualSpace> (Lounge) statt.\n\n";
$body
  .= "README zum Ablauf: <https://wiki.intern.chaosdorf.de/verein/plenum/>\n\n";
$body
  .= "Vorbereitetes Protokoll: <https://wiki.intern.chaosdorf.de/plenum/entry/${ymd}/>\n\n";
$body .= "Bisherige Tagesordnungspunkte:\n\n";

my $skip = 1;

for my $line ( read_file("plenum/entry/${ymd}.md") ) {
	$line = decode( 'utf-8', $line );
	if ( $skip and not $line =~ m{^#}o ) {
		next;
	}
	else {
		$skip = 0;
		if ( $line !~ $re_ignored ) {
			$body .= $line;
		}
	}
}

if ( not length($body) ) {
	exit 0;
}

$ymd =~ s{/}{-}g;

my $mail = Email::Simple->create(
	header => [
		To      => 'intern@chaosdorf.de',
		From    => '"Cron" <derf@chaosdorf.de>',
		Subject => encode_mimewords(
			encode( 'utf-8', "Plenumsankündigung $ymd" ),
			Charset => 'UTF-8'
		),
		'Content-Type' => 'text/plain; charset=UTF-8',
	],
	body => encode( 'utf-8', $body ),
);

sendmail($mail);
